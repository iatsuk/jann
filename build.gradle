configure(subprojects) {
    apply plugin: 'java-library'
    apply plugin: 'com.adarshr.test-logger'

    java {
        targetCompatibility = JavaVersion.VERSION_1_8
        sourceCompatibility = JavaVersion.VERSION_1_8
    }

    ext.versions = [
            jmh: '1.23',
    ]

    ext.deps = [
            // external ann
            annoy  : [
                    'com.spotify:annoy-jni:0.1.4',
                    'com.spotify.sparkey:sparkey:2.1.3',
                    'org.apache.commons:commons-lang3:3.5',
                    'com.google.guava:guava:15.0',
                    'org.xerial.snappy:snappy-java:1.1.2.1',
                    'org.slf4j:slf4j-api:1.7.2',
            ],
            annoyj : [
                    'com.spotify:annoy:0.2.6',
            ],
            annoy4s: [
                    'net.pishen:annoy4s_2.11:0.9.0',
                    'org.scala-lang:scala-library:2.11.12',
                    'com.github.pathikrit:better-files_2.11:3.8.0',
                    'net.java.dev.jna:jna:5.4.0',
            ],

            // data source
            csv    : 'net.sourceforge.javacsv:javacsv:2.0',

            // common
            guava  : 'com.google.guava:guava:15.0',

            // benchmarking
            jmh    : [
                    "org.openjdk.jmh:jmh-core:${versions.jmh}",
                    "org.openjdk.jmh:jmh-generator-annprocess:${versions.jmh}",
                    'net.sf.jopt-simple:jopt-simple:4.6',
                    'org.apache.commons:commons-math3:3.2',
            ],

            // tests
            junit  : [
                    'junit:junit:4.12',
                    'org.hamcrest:hamcrest-core:1.3',
            ],
    ]

    repositories {
        jcenter()
    }

    dependencies {
        testImplementation(
                deps.junit,
        )
    }

    configurations.all {
        transitive = false
        resolutionStrategy {
            failOnVersionConflict()
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += [
                "-Werror",
                "-Xlint",
                "-Xlint:-serial",
                "-Xlint:-path",
                "-Xlint:-deprecation",
                "-Xlint:-rawtypes"
        ]
        options.encoding = 'UTF-8'
    }

    testlogger {
        theme 'mocha'
    }

}
